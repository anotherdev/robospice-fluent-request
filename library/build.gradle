// Version
def versionMajor = 1
def versionMinor = 0 // [0-9] minor feature releases
def versionPatch = 0 // [0-9] This should be bumped for hot fixes
def versionBuild = 0 // [0-99] bump for dogfood builds, public betas, etc.

// Double check the versioning
assert 0 <= versionMajor
for (versionPart in [versionPatch, versionMinor]) {
    assert 0 <= versionPart
    assert versionPart <= 9
}
assert 0 <= versionBuild
assert versionBuild <= 99


apply plugin: 'com.android.library'

ext {
    PUBLISH_GROUP_ID = 'com.anotherdev.android.robospice'
    PUBLISH_ARTIFACT_ID = 'robospice-fluent-request'
    PUBLISH_VERSION = "${versionMajor}.${versionMinor}.${versionPatch}"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.octo.android.robospice:robospice:1.4.14'

    compile 'io.reactivex:rxjava:1.2.7'

    provided 'com.google.code.findbugs:jsr305:3.0.1'
}

// Local
//apply from: 'android-release-aar.gradle'

// Remote copy with latest changes
apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-jar.gradle'
